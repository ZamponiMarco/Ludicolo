package rules

//list any import classes here.
import java.util.Comparator
import it.unicam.lcp.ludicolo.*
import it.unicam.lcp.ludicolo.Utility
import it.unicam.lcp.ludicolo.pkmn.Stat
import it.unicam.lcp.ludicolo.actions.moves.MoveAction
import it.unicam.lcp.ludicolo.actions.moves.MoveType
import it.unicam.lcp.ludicolo.actions.ActionStatus
import it.unicam.lcp.ludicolo.pkmn.Pokemon
import it.unicam.lcp.ludicolo.pkmn.PokemonStatus
import it.unicam.lcp.ludicolo.actions.items.ItemAction
import it.unicam.lcp.ludicolo.actions.Action
import java.util.HashMap
import java.util.Map
import java.util.Random
import it.unicam.lcp.ludicolo.actions.moves.MoveEffectTarget
import it.unicam.lcp.ludicolo.actions.moves.StageMoveEffect
import it.unicam.lcp.ludicolo.actions.moves.StatusMoveEffect
import java.util.List
import it.unicam.lcp.ludicolo.view.App

rule "StatusDurationReduction"
    agenda-group "turn setup"
    salience 0
    no-loop true
when
    playerAction : Action(playerAction.status == ActionStatus.DEFINED)
    pokemon: Pokemon(pokemon.status != null, pokemon.owner.equals(playerAction.getSource()))
then
    Utility.helper(drools);
    pokemon.setStatusDuration(Math.max((pokemon.getStatusDuration() - 1),-1));
    update(pokemon);
end

rule "EffectTimeUp"
    agenda-group "action fire"
    salience 5
    no-loop true
when
    pokemon : Pokemon(pokemon.status != null, pokemon.statusDuration < 0)
then
    Utility.helper(drools);

    if(pokemon.getStatus().equals(PokemonStatus.PARALYZED)){
        Map<Stat, Integer> parBattleStats = new HashMap<>();
        parBattleStats.put(Stat.SPEED, Utility.calculateBattleStat(pokemon.getBaseStatValue(Stat.SPEED), pokemon.getLevel()));
        pokemon.setBattleStat(parBattleStats);
    }

    App.log(pokemon.getName() + "'s " + pokemon.getStatus() + " status wore off");
    pokemon.setStatus(null);
    update(pokemon);

end

rule "ProtectionTimeUp"
    agenda-group "turn ending"
    salience 0
    no-loop true
when
    not Action()
    pokemon: Pokemon(pokemon.protectedFromAttacks)
then
    Utility.helper(drools);
    pokemon.setProtectedFromAttacks(false);
    App.log(pokemon.getName() + " is no longer protected from attacks");
    update(pokemon);
end



rule "BurnedEffect"
    agenda-group "turn ending"
    salience 0
    no-loop true
when
    not Action()
    pokemon : Pokemon(pokemon.status == PokemonStatus.BURNED)
then
    Utility.helper(drools);
    System.out.println(pokemon.getName() + " is " + pokemon.getStatus());
    Map<Stat, Integer> lifeStageStats = new HashMap<>();
    int targetLife = pokemon.getStageValue(Stat.LIFE);
     int damage = pokemon.getBattleStatValue(Stat.LIFE)/16;
    System.out.println("Target life: " + targetLife);
    System.out.println("Damage: " + damage);

    lifeStageStats.put(Stat.LIFE, Math.max((targetLife - damage),0));
    App.log(pokemon.getName() + " gets damaged by burn");
    pokemon.setStage(lifeStageStats);
    update(pokemon);
    kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("winning conditions").setFocus();
 end

 rule "PoisonedEffect"
     agenda-group "turn ending"
     salience 0
     no-loop true
 when
     not Action()
     pokemon : Pokemon(pokemon.status == PokemonStatus.POISONED)
 then
     Utility.helper(drools);
     System.out.println(pokemon.getName() + " is " + pokemon.getStatus());
     Map<Stat, Integer> lifeStageStats = new HashMap<>();
     int targetLife = pokemon.getStageValue(Stat.LIFE);
     int damage = pokemon.getBattleStatValue(Stat.LIFE)/8;
     System.out.println("Target life: " + targetLife);
     System.out.println("Damage: " + damage);

     lifeStageStats.put(Stat.LIFE, Math.max((targetLife - damage),0));
     App.log(pokemon.getName() + " gets damaged by poison");
     pokemon.setStage(lifeStageStats);
     update(pokemon);
     kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("winning conditions").setFocus();
  end

rule "ConfusedEffect"
    agenda-group "move action fire"
    salience 3
    no-loop true
when
      moveAction: MoveAction(moveAction.status == ActionStatus.READY_TO_FIRE)
      pokemon : Pokemon(pokemon.status == PokemonStatus.CONFUSED, pokemon.owner.equals(moveAction.getSource()))
then
     Utility.helper(drools);
     App.log(pokemon.getName() + " is " + pokemon.getStatus());
     if(Utility.probabilityCheck(33)){
            Map<Stat, Integer> lifeStageStats = new HashMap<>();
            int targetLife = pokemon.getStageValue(Stat.LIFE);
            int dmg = Utility.calculateDamage(
                    pokemon.getLevel(),
                    40,
                    pokemon.getBattleStatValue(Stat.ATTACK),
                    pokemon.getBattleStatValue(Stat.DEFENSE),
                    1.0);
            lifeStageStats.put(Stat.LIFE, Math.max((targetLife - dmg),0));



            pokemon.setStage(lifeStageStats);
            App.log(pokemon.getName() + " gets damaged by confusion");
            System.out.println("DMG: "+dmg);
            update(pokemon);

            moveAction.setStatus(ActionStatus.DONE);
            App.log(pokemon.getName() + " failed to throw its move");
            update(moveAction);

            kcontext.getKnowledgeRuntime().getAgenda().getAgendaGroup("winning conditions").setFocus();
     }
 end

 rule "FrozenEffect"
     agenda-group "action fire"
     salience 3
     no-loop true
 when
      moveAction: MoveAction(moveAction.status == ActionStatus.READY_TO_FIRE)
      pokemon : Pokemon(pokemon.status == PokemonStatus.FROZEN, pokemon.owner.equals(moveAction.getSource()))
 then
      Utility.helper(drools);
      App.log(pokemon.getName() + " is " + pokemon.getStatus());
      moveAction.setStatus(ActionStatus.DONE);

      App.log(pokemon.getName() + " failed to throw its move");
      update(moveAction);
  end

 rule "AsleepEffect"
     agenda-group "action fire"
     salience 3
     no-loop true
 when
      moveAction: MoveAction(moveAction.status == ActionStatus.READY_TO_FIRE)
      pokemon : Pokemon(pokemon.status == PokemonStatus.ASLEEP, pokemon.owner.equals(moveAction.getSource()))
 then
      Utility.helper(drools);
      App.log(pokemon.getName() + " is " + pokemon.getStatus());
      moveAction.setStatus(ActionStatus.DONE);

      App.log(pokemon.getName() + " failed to throw its move");
      update(moveAction);
  end

rule "ParalyzedEffect"
     agenda-group "action fire"
     salience 3
     no-loop true
when
      moveAction: MoveAction(moveAction.status == ActionStatus.READY_TO_FIRE)
      pokemon : Pokemon(pokemon.status == PokemonStatus.PARALYZED, pokemon.owner.equals(moveAction.getSource()))
then
      Utility.helper(drools);
      App.log(pokemon.getName() + " is " + pokemon.getStatus());
    if(!Utility.probabilityCheck(25)) {
        moveAction.setStatus(ActionStatus.DONE);
        App.log(pokemon.getName() + " failed to throw its move");
        update(moveAction);
    }
 end

 rule "ProtectedEffect"
    agenda-group "move action fire"
    salience 2
    no-loop true
 when
      moveAction: MoveAction(moveAction.status == ActionStatus.READY_TO_FIRE)
      pokemonTarget : Pokemon(pokemonTarget.isProtectedFromAttacks(), pokemonTarget.owner.equals(moveAction.getTarget()))
      pokemonSource : Pokemon(pokemonSource.owner.equals(moveAction.getSource()))
 then
     Utility.helper(drools);
     App.log(pokemonTarget.getName() + " is protected.");

     moveAction.setStatus(ActionStatus.DONE);
     App.log(pokemonSource.getName() + " failed to throw its move");

     update(moveAction);
end
